;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;
;;;; ZdrojovÃ½ soubor k uÄebnÃ­mu textu M. Krupka: ObjektovÃ© programovÃ¡nÃ­
;;;;
;;;; Kapitola 8, knihovna OMG, verze 1.0
;;;;

#| 
PÅ™ed naÄtenÃ­m souboru naÄtÄ›te knihovnu micro-graphics
Pokud pÅ™i naÄÃ­tÃ¡nÃ­ (kompilaci) dojde k chybÄ› 
"Reader cannot find package MG",
znamenÃ¡ to, Å¾e knihovna micro-graphics nenÃ­ naÄtenÃ¡.
|#

#|

DOKUMENTACE
-----------

TÅ˜ÃDA OMG-OBJECT
----------------

JejÃ­mi potomky jsou vÅ¡echny tÅ™Ã­dy naÅ¡Ã­ grafickÃ© knihovny. NenÃ­ urÄena k
vytvÃ¡Å™enÃ­ pÅ™Ã­mÃ½ch instancÃ­. Implementuje vlastnickÃ© vztahy mezi objekty:
delegÃ¡ta, systÃ©m udÃ¡lostÃ­, hlÃ¡Å¡enÃ­ zmÄ›n a zÃ¡kladnÃ­ udÃ¡losti (ev-changing, 
ev-change, ev-mouse-down). Teorie kolem vlastnickÃ½ch vztahÅ¯ je popsÃ¡na 
v textu.

V rÃ¡mci systÃ©mu hlÃ¡Å¡enÃ­ zmÄ›n rozumÃ­ zprÃ¡vÃ¡m send-with-change, changing a 
change. SouÄÃ¡stÃ­ systÃ©mu hlÃ¡Å¡enÃ­ zmÄ›n jsou udÃ¡losti ev-changing a ev-change.


NOVÃ‰ VLASTNOSTI

delegate     Obsahuje delegÃ¡ta (neboli vlastnÃ­ka) objektu. Povoleno nastavovat
             pouze vlastnÃ­kem (obvykle v momentÄ›, kdy se objekt stÃ¡vÃ¡ jeho
             souÄÃ¡stÃ­).
change-level PoÄÃ­tadlo s hodnotami 0 a vÃ­ce. SlouÅ¾Ã­ k blokovÃ¡nÃ­ hlÃ¡Å¡enÃ­ zmÄ›n
             v metodÃ¡ch change a changing. Jen ke ÄtenÃ­, ale dÃ¡ se nastavovat 
             pomocÃ­ inc-change-level a dec-change-level.
             

NOVÃ‰ ZPRÃVY

send-event object event &rest event-args

ZprÃ¡vu posÃ­lÃ¡ objekt object sÃ¡m sobÄ›, kdyÅ¾ chce odeslat udÃ¡lost event s
argumenty event-args. Ve tÅ™Ã­dÄ› omg-object dÄ›lÃ¡ metoda nÃ¡sledujÃ­cÃ­:
- zkontroluje, Å¾e objekt mÃ¡ delegÃ¡ta (vlastnost delegate)
- zkontroluje, Å¾e delegÃ¡t implementuje metodu pro udÃ¡lost event
- udÃ¡lost odeÅ¡le (pÅ™Ã­padnÄ› pod pÅ™eloÅ¾enÃ½m jmÃ©nem)
- jako vÃ½sledek vrÃ¡tÃ­ object nebo hodnotu vrÃ¡cenou udÃ¡lostÃ­.

change object message changed-obj msg-args

ZprÃ¡vu posÃ­lÃ¡ objekt sÃ¡m sobÄ› potÃ©, co u nÄ›j doÅ¡lo ke zmÄ›nÄ›, kterou je tÅ™eba
oznÃ¡mit delegÃ¡tovi (udÃ¡lostÃ­ ev-change). Ve tÅ™Ã­dÄ› omg-object metoda testuje,
nenÃ­-li (poÄÃ­tadlem change-level) u objektu potlaÄeno posÃ­lÃ¡nÃ­ udÃ¡lostÃ­ ev-change
a ev-changing. Pokud nenÃ­, poÅ¡le (pomocÃ­ zprÃ¡vy send-event) udÃ¡lost ev-change.
Parametry:
- message      jmÃ©no zprÃ¡vy, kterÃ¡ zmÄ›nu vyvolala
- changed-obj  objekt, u nÄ›jÅ¾ doÅ¡lo ke zmÄ›nÄ› (mÅ¯Å¾e se liÅ¡it od objektu object)
- msg-args     argumenty zprÃ¡vy
Hodnoty tÄ›chto parametrÅ¯ se stanou argumenty udÃ¡losti ev-change.

changing object message changed-obj msg-args

ZprÃ¡vu posÃ­lÃ¡ objekt sÃ¡m sobÄ› pÅ™edtÃ­m, neÅ¾ u nÄ›j dojde ke zmÄ›nÄ›, kterou je 
tÅ™eba oznÃ¡mit delegÃ¡tovi (udÃ¡lostÃ­ ev-changing). Ve tÅ™Ã­dÄ› omg-object metoda 
testuje, nenÃ­-li (poÄÃ­tadlem change-level) u objektu potlaÄeno posÃ­lÃ¡nÃ­ udÃ¡lostÃ­ 
ev-change a ev-changing. Pokud nenÃ­, poÅ¡le (pomocÃ­ zprÃ¡vy send-event) udÃ¡lost
ev-changing.
Parametry:
- message      jmÃ©no zprÃ¡vy, kterÃ¡ zmÄ›nu vyvolala
- changed-obj  objekt, u nÄ›jÅ¾ dojde ke zmÄ›nÄ› (mÅ¯Å¾e se liÅ¡it od objektu object)
- msg-args     argumenty zprÃ¡vy
Hodnoty tÄ›chto parametrÅ¯ se stanou argumenty udÃ¡losti ev-changing.

send-with-change object msg reported-msg msg-args

ZaÅ™Ã­dÃ­ poslÃ¡nÃ­ zprÃ¡vy jmÃ©nem msg objektu object s argumenty msg-args. PÅ™edtÃ­m
pomocÃ­ zprÃ¡vy changing zaÅ™Ã­dÃ­ odeslÃ¡nÃ­ udÃ¡losti ev-changing a potom pomocÃ­
zprÃ¡vy change poslÃ¡nÃ­ udÃ¡losti ev-change. Jako zprÃ¡vu, kterÃ¡ vyvolÃ¡ (vyvolala)
zmÄ›nu pouÅ¾ije zprÃ¡vu reported-msg. Tedy, parametrem message udÃ¡losti ev-change
a ev-changing bude parametr reported-msg. Parametrem changed-obj bude objekt
object a parametrem msg-args bude hodnota msg-args.
- Pokud ovÅ¡em udÃ¡lost ev-changing vrÃ¡tÃ­ nil, znamenÃ¡ to, Å¾e delegÃ¡t objektu
  si nepÅ™eje, aby se zmÄ›na provedla. Proto se zprÃ¡vy msg a change nepoÅ¡lou.
- ZprÃ¡va msg se posÃ­lÃ¡ s inkrementovanÃ½m poÄÃ­tadlem change-level, coÅ¾ zabrÃ¡nÃ­
  generovÃ¡nÃ­ dalÅ¡Ã­ch podÅ™Ã­zenÃ½ch udÃ¡lostÃ­ o zmÄ›nÄ›. 

inc-change-level object
dec-change-level object

Inkrementace a dekrementace poÄÃ­tadla change-level.


NOVÃ‰ PÅ˜IJÃMANÃ‰ UDÃLOSTI

ev-change   receiver sender message changed-obj args
ev-changing receiver sender message changed-obj args

ImplementovÃ¡ny ve tÅ™Ã­dÄ› omg-object, aby jim kaÅ¾dÃ½ objekt rozumÄ›l. V tÃ©to tÅ™Ã­dÄ›
pouze pÅ™eposÃ­lajÃ­ udÃ¡lost delegÃ¡tovi pomocÃ­ zprÃ¡vy change (resp. changing).

ev-mouse-down receiver sender clicked-obj button position

PÅ™eposÃ­lÃ¡ udÃ¡lost delegÃ¡tovi.


NOVÃ‰ ZASÃLANÃ‰ UDÃLOSTI

ev-change   receiver sender message changing-obj args 
ev-changing receiver sender message changing-obj args

Tyto udÃ¡losti posÃ­lÃ¡ objekt delegÃ¡tovi, kdyÅ¾ u nÄ›j dochÃ¡zÃ­ ke zmÄ›nÄ›. UdÃ¡lost
ev-changing bezprostÅ™ednÄ› pÅ™ed zmÄ›nou, ev-change bezprostÅ™ednÄ› po (viz zprÃ¡vu
send-with-change). KromÄ› standardnÃ­ch parametrÅ¯ receiver a sender
(spoleÄnÃ½ch vÅ¡em udÃ¡lostem), jsou dalÅ¡Ã­mi parametry:
- message: zprÃ¡va, v dÅ¯sledku jejÃ­hoÅ¾ pÅ™ijetÃ­ ke zmÄ›nÄ› dochÃ¡zÃ­
- changing-obj: pÅ™Ã­jemce tÃ©to zprÃ¡vy, tj, objekt, u nÄ›jÅ¾ dochÃ¡zÃ­ ke zmÄ›nÄ›
- args: argumenty, se kterÃ½mi byla zprÃ¡va odeslÃ¡na (bez pÅ™Ã­jemce)
VyhodnocenÃ­m seznamu (message changing-obj . args) by se zopakovalo poslÃ¡nÃ­
zprÃ¡vy.

ev-mouse-down receiver sender clicked-obj button position

PoslÃ¡na, pokud objekt zjistÃ­, Å¾e na nÄ›j nebo na podÅ™Ã­zenÃ½ objekt uÅ¾ivatel 
klikl. Parametr clicked-obj je objekt, na kterÃ½ se kliklo pÅ¯vodnÄ› (mÅ¯Å¾e to 
bÃ½t podobjekt odesÃ­lajÃ­cÃ­ho objektu; pak se hodnoty sender a clicked-obj 
liÅ¡Ã­). button je :left, :center, nebo :right. position je bod (instance tÅ™Ã­dy
point), na kterÃ½ se kliklo.



TÅ˜ÃDA SHAPE (OMG-OBJECT)
------------------------

Potomky tÅ™Ã­dy shape jsou vÅ¡echny tÅ™Ã­dy grafickÃ½ch objektÅ¯. Sama nenÃ­ urÄena k
vytvÃ¡Å™enÃ­ pÅ™Ã­mÃ½ch instancÃ­.


NOVÃ‰ VLASTNOSTI

window    Okno, ve kterÃ©m je objekt umÃ­stÄ›n, nebo nil. Typicky ho instance 
          pouÅ¾Ã­vajÃ­ pÅ™i kreslenÃ­. Nastavovat ho smÃ­ pouze nadÅ™Ã­zenÃ© objekty
shape-mg-window 
          mg-window okna window.
color, thickness, filledp     
          GrafickÃ© parametry. Metody nastavujÃ­cÃ­ tyto hodnoty zajiÅ¡Å¥ujÃ­
          signalizaci zmÄ›n udÃ¡lostmi ev-changing a ev-change a zasÃ­lajÃ­
          objektu pomocnou zprÃ¡vu do-set-color (-thickness, -filledp).
          DÄ›lajÃ­ to pÅ™i inkrementovanÃ©m vnitÅ™nÃ­m poÄÃ­tadle, takÅ¾e v rÃ¡mci
          obsluhy tÄ›chto pomocnÃ½ch zprÃ¡v objekt dalÅ¡Ã­ udÃ¡losti o zmÄ›nÃ¡ch
          neposÃ­lÃ¡. Metody set-color, set-thickness, set-filledp nejsou urÄeny
          k pÅ™episovÃ¡nÃ­. PÅ™episujte metody do-set-color atd.
solidp    Zda objekt pÅ™ijÃ­mÃ¡ zprÃ¡vu mouse-down. Ve tÅ™Ã­dÄ› shape je vÅ¾dy t.
solid-shapes
          Seznam podobjektÅ¯ (vÄetnÄ› objektu samÃ©ho) s nastavenou vlastnostÃ­
          solidp. Je-li solidp Pravda uÅ¾ u tohoto objektu, je to jednoprvkovÃ½
          seznam s objektem samotnÃ½m. Jinak je to hodnota vlastnosti
          solid-subshapes.
solid-subshapes
          Seznam podobjektÅ¯ (mimo objektu samÃ©ho) s nastavenou vlastnostÃ­
          solidp. ZjiÅ¡Å¥uje se jen u objektÅ¯, kterÃ© majÃ­ vlastnost solidp nil.
          Pokud mÃ¡ objekt vlastnost solidp nastavenu na Pravda, musÃ­ mÃ­t 
          implementovÃ¡nu metodu solid-subshapes. Ve tÅ™Ã­dÄ› shape vede k chybÄ›.

NOVÃ‰ ZPRÃVY

do-set-color object value
do-set-thickness object value
do-set-filledp object value

NastavujÃ­ hodnoty pÅ™Ã­sluÅ¡nÃ½ch vlastnostÃ­. Jsou volÃ¡ny z metod set-color,
set-thickness, set-filledp tÅ™Ã­dy shape. Tyto metody mohou potomci pÅ™epsat a
modifikovat tak jejich chovÃ¡nÃ­. Nejsou urÄeny k volÃ¡nÃ­. 

move object dx dy
rotate object angle center
scale object coeff center

RealizujÃ­ geometrickÃ© transformace objektu object. Ve tÅ™Ã­dÄ› shape zaÅ™Ã­dÃ­
signalizaci zmÄ›n a zasÃ­lajÃ­ pÅ™Ã­sluÅ¡nÃ© do-zprÃ¡vy. BÄ›hem jejich vykonÃ¡vÃ¡nÃ­ je
signalizace zmÄ›n potlaÄena vnitÅ™nÃ­m poÄÃ­tadlem.

do-move object dx dy
do-rotate object angle center
do-scale object coeff center

Potomci tÅ™Ã­dy shape pÅ™episujÃ­ tyto metody a implementujÃ­ v nich pÅ™Ã­sluÅ¡nÃ©
geometrickÃ© transformace. Tyto zprÃ¡vy nejsou urÄeny k pÅ™Ã­mÃ©mu volÃ¡nÃ­; jsou
volÃ¡ny z metod move, rotate, scale tÅ™Ã­dy shape, kterÃ© vytvÃ¡Å™Ã­ sprÃ¡vnÃ© 
prostÅ™edÃ­ pro hlÃ¡Å¡enÃ­ zmÄ›n. Ve tÅ™Ã­dÄ› shape metody nedÄ›lajÃ­ nic.

draw shape

VykreslÃ­ shape do jejÃ­ho okna. Typicky volÃ¡no nadÅ™Ã­zenÃ½m objektem. Ve tÅ™Ã­dÄ› 
shape zasÃ­lÃ¡ zprÃ¡vy set-mg-params a do-draw.

set-mg-params shape

NastavÃ­ kreslicÃ­ parametry okna knihovny micro-graphics, ve kterÃ©m je objekt 
umÃ­stÄ›n, tak, aby mohl bÃ½t pozdÄ›ji vykreslen. Ve tÅ™Ã­dÄ› shape nastavuje 
parametry :foreground, :filledp, :thickness podle hodnot vlastnostÃ­ color,
filledp a thickness (poÅ™adÄ›). ZasÃ­lÃ¡ se z metody draw tÅ™Ã­dy shape. NenÃ­ nutnÃ©
volat jindy. Potomci mohou pÅ™epsat.

do-draw shape

VykreslÃ­ objekt shape do jeho okna. MÅ¯Å¾e poÄÃ­tat s tÃ­m, Å¾e grafickÃ© parametry
okna knihovny micro-graphics jsou uÅ¾ nastaveny (metodou set-mg-params).
ZasÃ­lÃ¡ se z metody draw tÅ™Ã­dy shape. NenÃ­ nutnÃ© volat jindy. Potomci mohou 
pÅ™epsat. Ve tÅ™Ã­dÄ› shape nedÄ›lÃ¡ nic.

contains-point-p shape point

VracÃ­ logickou hodnotu "shape obsahuje point". Ve tÅ™Ã­dÄ› shape vracÃ­ nil.

mouse-down object button position

Objekt obdrÅ¾Ã­ tuto zprÃ¡vu od okna potÃ©, co do nÄ›j uÅ¾ivatel klikl myÅ¡Ã­. 
button je :left, :center, nebo :right, position bod. Lze se
spolehnout, Å¾e bod position je opravdu uvnitÅ™ objektu. ZprÃ¡vu dostÃ¡vajÃ­
pouze objekty s nastavenou vlastnostÃ­ solidp. Metoda tÅ™Ã­dy
shape posÃ­lÃ¡ udÃ¡lost ev-mouse-down s argumentem clicked-obj rovnÃ½m 
object.


TÅ˜ÃDA POINT (SHAPE)
-------------------

GeometrickÃ½ bod s kartÃ©zskÃ½mi a polÃ¡rnÃ­mi souÅ™adnicemi. KreslÃ­ se jako
vyplnÄ›nÃ© koleÄko.


NOVÃ‰ VLASTNOSTI

x, y, r, phi  SouÅ™adnice. NastavenÃ­ vyvolÃ¡ hlÃ¡Å¡enÃ­ o zmÄ›nÄ›.

NOVÃ‰ ZPRÃVY

set-r-phi point r phi

SoubÄ›Å¾nÃ© nastavenÃ­ obou polÃ¡rnÃ­ch souÅ™adnic. VyvolÃ¡vÃ¡ hlÃ¡Å¡enÃ­ o zmÄ›nÄ›.

PÅ˜EPSANÃ‰ METODY

do-move point dx dy
do-rotate point angle center
do-scale point coeff center

ImplementujÃ­ geometrickÃ© transformace bodu.

set-mg-params point

Modifikuje zdÄ›dÄ›nou metodu tak, Å¾e nastavuje :filledp na T.

do-draw point

VykreslÃ­ bod jako vyplnÄ›nÃ© koleÄko o polomÄ›ru rovnÃ©m nastavenÃ© thickness.

contains-point-p point point2

VracÃ­ true prÃ¡vÄ› kdyÅ¾ je vzdÃ¡lenost bodÅ¯ <= (thickness point).


TÅ˜ÃDA CIRCLE (SHAPE)
--------------------

KoleÄka se stÅ™edem (instance tÅ™Ã­dy point) a polomÄ›rem. StÅ™edu lze nastavovat
souÅ™adnice a aplikovat na nÄ›j transformace.

NOVÃ‰ VLASTNOSTI

center   StÅ™ed. Instance tÅ™Ã­dy point. Jen ke ÄtenÃ­.
radius   PolomÄ›r.


PÅ˜EPSANÃ‰ METODY

do-move circle dx dy
do-rotate circle angle center
do-scale circle coeff center

ImplementujÃ­ geometrickÃ© transformace kruhu.

contains-point-p circle point

Je-li (filledp circle) true, vracÃ­ true prÃ¡vÄ› kdyÅ¾ je vzdÃ¡lenost bodu point
od stÅ™edu <= (radius circle). Je-li (filledp circle) false, vracÃ­ true prÃ¡vÄ› 
kdyÅ¾ point leÅ¾Ã­ na kruÅ¾nici s polomÄ›rem (radius circle) a stÅ™edem (center 
circle) o tlouÅ¡Å¥ce (thickness circle).


TÅ˜ÃDA COMPOUND-SHAPE (SHAPE)
----------------------------

PÅ™edek tÅ™Ã­d grafickÃ½ch objektÅ¯, sloÅ¾enÃ½ch z jinÃ½ch grafickÃ½ch objektÅ¯.

NOVÃ‰ VLASTNOSTI

items   Seznam podobjektÅ¯. PÅ™i nastavovÃ¡nÃ­ otestuje validitu nastavovanÃ½ch
        podobjektÅ¯ (zprÃ¡vou check-items), pÅ™ipravÃ­ hlÃ¡Å¡enÃ­ zmÄ›ny pÅ™ed a po 
        nastavenÃ­ a poÅ¡le zprÃ¡vu do-set-items s inkrementovanÃ½m poÄÃ­tadlem
        pro zmÄ›ny. PÅ™i ÄtenÃ­ vracÃ­ kopii seznamu.


NOVÃ‰ ZPRÃVY

check-item shape item

Otestuje, zda objekt item mÅ¯Å¾e bÃ½t podobjektem sloÅ¾enÃ©ho objektu shape. Pokud
ne, vyvolÃ¡ chybu. Ve tÅ™Ã­dÄ› compound-shape vyvolÃ¡vÃ¡ chybu poÅ™Ã¡d (abstraktnÃ­
metoda), potomci metodu musÃ­ pÅ™epsat.

check-items shape items

Otestuje, zda seznam items mÅ¯Å¾e bÃ½t seznamem podobjektÅ¯ objektu shape. Pokud
ne, vyvolÃ¡ chybu. Ve tÅ™Ã­dÄ› compound-shape posÃ­lÃ¡ zprÃ¡vu check-item pro kaÅ¾dÃ½
prvek seznamu items.

do-set-items shape value

NastavÃ­ kopii seznamu value jako vlastnost items sloÅ¾enÃ©ho objektu shape.
Seznam uÅ¾ je provÄ›Å™enÃ½ zprÃ¡vou check-items. NenÃ­ urÄeno k volÃ¡nÃ­. Je volÃ¡no z
metody set-items tÅ™Ã­dy compound-shape po nastavenÃ­ prostÅ™edÃ­ pro hlÃ¡Å¡enÃ­ zmÄ›n.
Ve tÅ™Ã­dÄ› compound shape kromÄ› vlastnÃ­ho nastavenÃ­ vlastnosti items nastavuje
vÅ¡em objektÅ¯m v seznamu vlastnosti window a delegate.

send-to-items shape message &rest arguments

Lze pouÅ¾Ã­t, pokud chceme vÅ¡em podobjektÅ¯m sloÅ¾enÃ©ho objektu shape poslat
tutÃ©Å¾ zprÃ¡vu se stejnÃ½mi argumenty.


PÅ˜EPSANÃ‰ METODY

do-move shape dx dy
do-rotate shape angle center
do-scale shape coeff center

PomocÃ­ zprÃ¡vy send-to-items pÅ™epoÅ¡le zprÃ¡vu move (nebo rotate, nebo scale) 
vÅ¡em podobjektÅ¯m.


TÅ˜ÃDA PICTURE (COMPOUND-SHAPE)
------------------------------

SloÅ¾enÃ½ grafickÃ½ objekt, kterÃ½ se vykresluje tak, Å¾e postupnÄ› vykreslÃ­ vÅ¡echny
podobjekty.


PÅ˜EPSANÃ‰ VLASTNOSTI

window   PÅ™i nastavovÃ¡nÃ­ automaticky nastavÃ­ totÃ©Å¾ okno i vÅ¡em podobjektÅ¯m


PÅ˜EPSANÃ‰ METODY

check-item picture item

VyvolÃ¡ chybu, pokud item nenÃ­ typu shape.

do-draw picture

PoÅ¡le zprÃ¡vu draw vÅ¡em podobjektÅ¯m (v opaÄnÃ©m poÅ™adÃ­, aby se vykreslily zezadu
dopÅ™edu)

contains-point-p picture point

ProchÃ¡zÃ­ podobjekty a pÅ™eposÃ­lÃ¡ jim zprÃ¡vu contains-point-p. Pokud pro nÄ›kterÃ½
podobjekt je vÃ½sledek true, vrÃ¡tÃ­ true. Jinak false.


TÅ˜ÃDA POLYGON (COMPOUND-SHAPE)
------------------------------

GrafickÃ½ objekt sloÅ¾enÃ½ z bodÅ¯. Vykresluje se jako polygon. Proti 
compound-shape mÃ¡ novou vlastnost: closedp.

NOVÃ‰ VLASTNOSTI

closedp  PÅ™i filledp nastavenÃ©m na false urÄuje, zda do polygonu patÅ™Ã­ i
         ÃºseÄka spojujÃ­cÃ­ prvnÃ­ a poslednÃ­ bod. PÅ™i nastavovÃ¡nÃ­ nachystÃ¡
         prostÅ™edÃ­ hlÃ¡Å¡enÃ­ zmÄ›n a vlastnÃ­ nastavenÃ­ provede zaslÃ¡nÃ­m zprÃ¡vy
         do-set-closedp.


NOVÃ‰ ZPRÃVY

do-set-closedp polygon value

VlastnÃ­ nastavenÃ­ vlastnosti closedp. VolÃ¡no z metody set-closedp tÅ™Ã­dy
polygon, kterÃ¡ pÅ™ipravuje prostÅ™edÃ­ pro hlÃ¡Å¡enÃ­ zmÄ›n. Nevolat pÅ™Ã­mo, pouze
pÅ™episovat.

PÅ˜EPSANÃ‰ METODY

check-item polygon item

VyvolÃ¡ chybu, pokud item nenÃ­ bod.

set-mg-params polygon

VolÃ¡ zdÄ›dÄ›nou metodu a pak nastavuje kreslicÃ­ parametr closedp.

do-draw polygon

VykreslÃ­ polygon zavolÃ¡nÃ­m mg:draw-polygon.

contains-point-p polygon point

VracÃ­, zda polygon obsahuje bod point. Bere v Ãºvahu vlastnosti closedp,
filledp, thickness.


TÅ˜ÃDA WINDOW (OMG-OBJECT)
------------------------

Instance reprezentujÃ­ okna knihovny micro-graphics. Okno se otevÅ™e automaticky
pÅ™i vytvÃ¡Å™enÃ­ novÃ© instance. Oknu lze nastavit barvu pozadÃ­ a vykreslovanÃ½
grafickÃ½ objekt.


NOVÃ‰ VLASTNOSTI

shape      GrafickÃ½ objekt vykreslovanÃ½ do okna. PÅ™i nastavenÃ­ se nachystÃ¡
           prostÅ™edÃ­ na hlÃ¡Å¡enÃ­ zmÄ›n a pak se zavolÃ¡ do-set-shape. Potom
           se okno oznaÄÃ­ k pÅ™ekreslenÃ­ zprÃ¡vou invalidate.
background Barva pozadÃ­ okna. PÅ™i nastavenÃ­ se nachystÃ¡ prostÅ™edÃ­ na hlÃ¡Å¡enÃ­
           zmÄ›n a pak se zavolÃ¡ do-set-background. Potom se okno oznaÄÃ­ k 
           pÅ™ekreslenÃ­ zprÃ¡vou invalidate.
mg-window  Odkaz na okno knihovny micro-graphics. Jen ke ÄtenÃ­.


NOVÃ‰ ZPRÃVY

do-set-shape window shape

VlastnÃ­ nastavenÃ­ vlastnosti shape. Po nastavenÃ­ vlastnosti se objektu shape
nastavÃ­ vlastnost window a delegate na window a udÃ¡lost ev-change.

do-set-background window color

VlastnÃ­ nastavenÃ­ vlastnosti background.

invalidate window

PosÃ­lÃ¡ se oknu, pokud je tÅ™eba ho pÅ™ekreslit. OznaÄÃ­ okno k pÅ™ekreslenÃ­ a 
nÄ›kdy pozdÄ›ji okno dostane zprÃ¡vu redraw.

change window message changed-obj args

ZavolÃ¡ zdÄ›dÄ›nou metodu a pak invalidate.

redraw window

VymaÅ¾e okno barvou pozadÃ­ (vlastnost background) a pak vykreslÃ­ do okna objekt
uloÅ¾enÃ½ ve vlastnosti shape tÃ­m, Å¾e mu poÅ¡le zprÃ¡vu draw. Nevolat pÅ™Ã­mo, 
pouÅ¾Ã­vat zprÃ¡vu invalidate.

install-callbacks window

PosÃ­lÃ¡ se oknu jako souÄÃ¡st inicializace. SlouÅ¾Ã­ k instalaci zpÄ›tnÃ½ch
volÃ¡nÃ­ knihovny micro-graphics. Ve tÅ™Ã­dÄ› window instaluje zpÄ›tnÃ¡ volÃ¡nÃ­
:display a :mouse-down pomocÃ­ zprÃ¡v install-display-callback a
install-mouse-down-callback.

install-display-callback window
install-mouse-down-callback window

NainstalujÃ­ pÅ™Ã­sluÅ¡nÃ¡ zpÄ›tnÃ¡ volÃ¡nÃ­.

window-mouse-down window button position

Tuto zprÃ¡vu okno dostane, pokud do nÄ›j uÅ¾ivatel klikne myÅ¡Ã­. Parametr
button je :left, :center, nebo :right, position je bod, urÄujÃ­cÃ­ pozici myÅ¡i
pÅ™i kliknutÃ­. Metoda ve tÅ™Ã­dÄ› window zjistÃ­, zda se kliklo dovnitÅ™ grafickÃ©ho
objektu v oknÄ›, kterÃ½ mÃ¡ nastavenu vlastnost solidp na Pravda. Pokud ano, 
poÅ¡le si zprÃ¡vu mouse-down-inside-shape, jinak si poÅ¡le zprÃ¡vu
mouse-down-no-shape. NenÃ­ urÄeno k pÅ™Ã­mÃ©mu volÃ¡nÃ­.


PÅ˜IJÃMANÃ‰ UDÃLOSTI

ev-change

Ve tÅ™Ã­dÄ› window poÅ¡le oknu zprÃ¡vu invalidate, aby se vyvolalo jeho
pÅ™ekreslenÃ­. VolÃ¡ zdÄ›dÄ›nou metodu.
|#

;;;
;;; TÅ™Ã­da omg-object
;;;

(defclass omg-object () 
  ((delegate :initform nil)
   (change-level :initform 0)))

(defmethod delegate ((obj omg-object))
  (slot-value obj 'delegate))

(defmethod set-delegate ((obj omg-object) delegate)
  (setf (slot-value obj 'delegate) delegate))


(defmethod change-level ((obj omg-object))
  (slot-value obj 'change-level))

(defmethod inc-change-level ((obj omg-object))
  (setf (slot-value obj 'change-level)
        (+ (slot-value obj 'change-level) 1))
  obj)

(defmethod dec-change-level ((obj omg-object))
  (setf (slot-value obj 'change-level)
        (- (slot-value obj 'change-level) 1))
  obj)


;;Funkce has-method-p zjiÅ¡Å¥uje, zda existuje metoda pro danou
;;zprÃ¡vu a danÃ© argumenty. NenÃ­ nutnÃ© jÃ­ rozumÄ›t.
(defun has-method-p (object message arguments)
  (and (fboundp message)              ;je se symbolem message 
                                      ;svÃ¡zÃ¡na zprÃ¡va (funkce)?
       (compute-applicable-methods    ;vypoÄte seznam metod
        (symbol-function message)     ;pro zprÃ¡vu svÃ¡zanou s message
        (cons object arguments))))    ;s danÃ½mi argumenty


;; posÃ­lÃ¡nÃ­ udÃ¡lostÃ­: send-event

(defmethod send-event ((object omg-object) event 
		       &rest event-args)
  (let ((delegate (delegate object)))
    (if (and delegate 
             (has-method-p delegate 
                           event 
                           (cons object event-args)))
        (apply event delegate object event-args)
      object)))

(defmethod change ((object omg-object) message changed-obj args)
  (if (= (change-level object) 0)
      (send-event object 'ev-change message changed-obj args)
    object))

(defmethod changing ((object omg-object) message changing-obj args)
  (if (= (change-level object) 0)
      (send-event object 'ev-changing message changing-obj args)
    object))

(defmethod send-with-change 
           ((obj omg-object) msg reported-msg args)
  (when (changing obj reported-msg obj args)
    (unwind-protect
        (progn (inc-change-level obj)
          (apply 'funcall msg obj args))
      (dec-change-level obj))
    (change obj reported-msg obj args))
  obj)


;; zÃ¡kladnÃ­ udÃ¡losti

(defmethod ev-change 
           ((obj omg-object) sender msg orig-obj args)
  (change obj msg orig-obj args))

(defmethod ev-changing 
           ((obj omg-object) sender msg orig-obj args)
  (changing obj msg orig-obj args))

(defmethod ev-mouse-down 
           ((obj omg-object) sender clicked button position)
  (send-event obj 'ev-mouse-down clicked button position))


;;;
;;; TÅ™Ã­da shape
;;;

(defclass shape (omg-object)
  ((color :initform :black)
   (thickness :initform 1)
   (filledp :initform nil)
   (window :initform nil)))

(defmethod window ((shape shape)) 
  (slot-value shape 'window))

(defmethod set-window ((shape shape) value) 
  (setf (slot-value shape 'window) value)
  shape)

(defmethod shape-mg-window ((shape shape))
  (when (window shape)
    (mg-window (window shape))))

(defmethod color ((shape shape)) 
  (slot-value shape 'color))

(defmethod do-set-color ((shape shape) value)
  (setf (slot-value shape 'color) value))


(defmethod set-color ((shape shape) value) 
  (send-with-change shape 
                    'do-set-color 'set-color 
                    `(,value)))

(defmethod thickness ((shape shape)) 
  (slot-value shape 'thickness)) 

(defmethod do-set-thickness ((shape shape) value) 
  (setf (slot-value shape 'thickness) value)) 


(defmethod set-thickness ((shape shape) value)
  (send-with-change shape 
                    'do-set-thickness 'set-thickness 
                    `(,value)))

(defmethod filledp ((shape shape))
  (slot-value shape 'filledp))

(defmethod do-set-filledp ((shape shape) value)
  (setf (slot-value shape 'filledp) value))


(defmethod set-filledp ((shape shape) value)
  (send-with-change shape 
                    'do-set-filledp 'set-filledp 
                    `(,value)))

(defmethod do-move ((shape shape) dx dy)
  shape)

(defmethod move ((shape shape) dx dy)
  (send-with-change shape 
                    'do-move 'move 
                    `(,dx ,dy)))

(defmethod do-rotate ((shape shape) angle center)
  shape)


(defmethod rotate ((shape shape) angle center)
  (send-with-change shape 
                    'do-rotate 'rotate 
                    `(,angle ,center)))

(defmethod do-scale ((shape shape) coeff center)
  shape)


(defmethod scale ((shape shape) coeff center)
  (send-with-change shape 
                    'do-scale 'scale 
                    `(,coeff ,center)))

(defmethod set-mg-params ((shape shape)) 
  (let ((mgw (shape-mg-window shape)))
    (mg:set-param mgw :foreground (color shape)) 
    (mg:set-param mgw :filledp (filledp shape))
    (mg:set-param mgw :thickness (thickness shape)))
  shape)

(defmethod do-draw ((shape shape)) 
  shape)

(defmethod draw ((shape shape))
  (set-mg-params shape)
  (do-draw shape))


;;; PrÃ¡ce s myÅ¡Ã­

(defmethod solidp ((shape shape))
  t)

(defmethod solid-shapes ((shape shape))
  (if (solidp shape)
      (list shape)
    (solid-subshapes shape)))

(defmethod solid-subshapes ((shape shape))
  (error "Method has to be rewritten."))

(defmethod contains-point-p ((shape shape) point)
  nil)

(defmethod mouse-down ((shape shape) button position)
  (send-event shape 'ev-mouse-down shape button position))


;;;
;;; TÅ™Ã­da point
;;;

(defclass point (shape) 
  ((x :initform 0) 
   (y :initform 0)))

(defmethod x ((point point))
  (slot-value point 'x))

(defmethod y ((point point))
  (slot-value point 'y))

(defmethod do-set-x ((point point) value)
  (setf (slot-value point 'x) value))

(defmethod set-x ((point point) value)
  (unless (typep value 'number)
    (error "x coordinate of a point should be a number"))
  (send-with-change point 
                    'do-set-x 'set-x 
                    `(,value)))

(defmethod do-set-y ((point point) value)
  (setf (slot-value point 'y) value))


(defmethod set-y ((point point) value)
  (unless (typep value 'number)
    (error "y coordinate of a point should be a number"))
  (send-with-change point 
                    'do-set-y 'set-y 
                    `(,value)))

(defmethod r ((point point)) 
  (let ((x (slot-value point 'x)) 
        (y (slot-value point 'y))) 
    (sqrt (+ (* x x) (* y y)))))

(defmethod phi ((point point)) 
  (let ((x (slot-value point 'x)) 
        (y (slot-value point 'y))) 
    (cond ((> x 0) (atan (/ y x))) 
          ((< x 0) (+ pi (atan (/ y x)))) 
          (t (* (signum y) (/ pi 2))))))

(defmethod do-set-r-phi ((point point) r phi)
  (set-x point (* r (cos phi)))
  (set-y point (* r (sin phi))))

(defmethod set-r-phi ((point point) r phi) 
  (send-with-change point 
                    'do-set-r-phi 'set-r-phi 
                    `(,r ,phi)))

(defmethod do-set-r ((point point) value)
  (set-r-phi point value (phi point)))

(defmethod set-r ((point point) value) 
  (send-with-change point 
                    'do-set-r 'set-r 
                    `(,value)))

(defmethod do-set-phi ((point point) value)
  (set-r-phi point (r point) value))

(defmethod set-phi ((point point) value) 
  (send-with-change point 
                    'do-set-phi 'set-phi
                    `(,value)))

(defmethod set-mg-params ((pt point))
  (call-next-method)
  (mg:set-param (shape-mg-window pt) :filledp t)
  pt)

(defmethod do-draw ((pt point)) 
  (mg:draw-circle (shape-mg-window pt) 
                  (x pt) 
                  (y pt) 
                  (thickness pt))
  pt)

(defmethod do-move ((pt point) dx dy)
  (set-x pt (+ (x pt) dx))
  (set-y pt (+ (y pt) dy))
  pt)

(defmethod do-rotate ((pt point) angle center)
  (let ((cx (x center))
        (cy (y center)))
    (move pt (- cx) (- cy))
    (set-phi pt (+ (phi pt) angle))
    (move pt cx cy)
    pt))

(defmethod do-scale ((pt point) coeff center)
  (let ((cx (x center))
        (cy (y center)))
    (move pt (- cx) (- cy))
    (set-r pt (* (r pt) coeff))
    (move pt cx cy)
    pt))


;; PrÃ¡ce s myÅ¡Ã­

;; PomocnÃ© funkce (vzdÃ¡lenost bodÅ¯)

(defun sqr (x)
  (expt x 2))

(defun point-dist (pt1 pt2)
  (sqrt (+ (sqr (- (x pt1) (x pt2)))
           (sqr (- (y pt1) (y pt2))))))

(defmethod contains-point-p ((shape point) point)
  (<= (point-dist shape point) 
      (thickness shape)))


;;;
;;; TÅ™Ã­da circle
;;;

(defclass circle (shape) 
  ((center :initform (make-instance 'point)) 
   (radius :initform 1)))

(defmethod initialize-instance ((c circle) &key)
  (call-next-method)
  (set-delegate (center c) c))

(defmethod radius ((c circle))
  (slot-value c 'radius))

(defmethod do-set-radius ((c circle) value)
  (setf (slot-value c 'radius) value))

(defmethod set-radius ((c circle) value)
  (when (< value 0)
    (error "Circle radius should be a non-negative number"))
  (send-with-change c 
                    'do-set-radius 'set-radius 
                    `(,value)))

(defmethod center ((c circle))
  (slot-value c 'center))

(defmethod do-draw ((c circle))
  (mg:draw-circle (shape-mg-window c)
                  (x (center c))
                  (y (center c))
                  (radius c))
  c)

(defmethod do-move ((c circle) dx dy)
  (move (center c) dx dy)
  c)

(defmethod do-rotate ((c circle) angle center)
  (rotate (center c) angle center)
  c)

(defmethod do-scale ((c circle) coeff center)
  (scale (center c) coeff center)
  (set-radius c (* (radius c) coeff))
  c)


;; PrÃ¡ce s myÅ¡Ã­

(defmethod contains-point-p ((circle circle) point)
  (let ((dist (point-dist (center circle) point))
        (half-thickness (/ (thickness circle) 2)))
    (if (filledp circle)
        (<= dist (radius circle))
      (<= (- (radius circle) half-thickness)
          dist
          (+ (radius circle) half-thickness)))))


;;;
;;; TÅ™Ã­da compound-shape
;;;

(defclass compound-shape (shape)
  ((items :initform '())))

(defmethod items ((shape compound-shape)) 
  (copy-list (slot-value shape 'items)))

(defmethod send-to-items ((shape compound-shape) 
			  message
			  &rest arguments)
  (dolist (item (items shape))
    (apply message item arguments))
  shape)

(defmethod check-item ((shape compound-shape) item)
  (error "Abstract method."))

(defmethod check-items ((shape compound-shape) item-list)
  (dolist (item item-list)
    (check-item shape item))
  shape)

(defmethod do-set-items ((shape compound-shape) value)
  (setf (slot-value shape 'items) (copy-list value))
  (send-to-items shape #'set-delegate shape))

(defmethod set-items ((shape compound-shape) value)
  (check-items shape value)
  (send-with-change shape 
                    'do-set-items 'set-items 
                    `(,value)))

(defmethod do-move ((shape compound-shape) dx dy)
  (send-to-items shape #'move dx dy)
  shape)

(defmethod do-rotate ((shape compound-shape) angle center)
  (send-to-items shape #'rotate angle center)
  shape)

(defmethod do-scale ((shape compound-shape) coeff center)
  (send-to-items shape #'scale coeff center)
  shape)


;;;
;;; TÅ™Ã­da picture
;;;

(defclass picture (compound-shape)
  ())

(defmethod check-item ((pic picture) item)
  (unless (typep item 'shape)
    (error "Invalid picture element type."))
  pic)

(defmethod do-set-items ((shape picture) value)
  (call-next-method)
  (send-to-items shape 'set-window (window shape)))

(defmethod draw ((pic picture))
  (dolist (item (reverse (items pic)))
    (draw item))
  pic)

(defmethod set-window ((shape picture) value)
  (send-to-items shape 'set-window value)
  (call-next-method))

;; PrÃ¡ce s myÅ¡Ã­

(defmethod solidp ((pic picture))
  nil)

(defmethod solid-subshapes ((shape picture))
  (mapcan 'solid-shapes (items shape)))

(defmethod contains-point-p ((pic picture) point)
  (find-if (lambda (item)
	     (contains-point-p item point))
	   (items pic)))


;;;
;;; TÅ™Ã­da polygon
;;;

(defclass polygon (compound-shape)
  ((closedp :initform t)))

(defmethod check-item ((poly polygon) item)
  (unless (typep item 'point) 
    (error "Items of polygon should be points."))
  poly)

(defmethod closedp ((p polygon))
  (slot-value p 'closedp))

(defmethod do-set-closedp ((p polygon) value)
  (setf (slot-value p 'closedp) value))

(defmethod set-closedp ((p polygon) value)
  (send-with-change p 
                    'do-set-closedp 'set-closedp
                    `(,value)))

(defmethod set-mg-params ((poly polygon)) 
  (call-next-method)
  (mg:set-param (shape-mg-window poly) 
                :closedp
                (closedp poly))
  poly)

(defmethod polygon-coordinates ((p polygon))
  (let (coordinates)
    (dolist (point (reverse (items p)))
      (setf coordinates (cons (y point) coordinates)
            coordinates (cons (x point) coordinates)))
    coordinates))

(defmethod do-draw ((poly polygon)) 
  (mg:draw-polygon (shape-mg-window poly) 
                   (polygon-coordinates poly))
  poly)


;;
;; contains-point-p pro polygon vyuÅ¾Ã­vÃ¡ funkci
;; mg:point-in-polygon-p knihovny micro-graphics.
;;

(defmethod contains-point-p ((poly polygon) point)
  (mg:point-in-polygon-p (x point) (y point) 
                         (closedp poly)
                         (filledp poly) 
                         (thickness poly)
                         (polygon-coordinates poly)))

;;;
;;; TÅ™Ã­da window
;;;

(defclass window (omg-object)
  ((mg-window :initform (mg:display-window))
   (shape :initform nil)
   (background :initform :white)))

(defmethod mg-window ((window window)) 
  (slot-value window 'mg-window))

(defmethod shape ((w window))
  (slot-value w 'shape))

(defmethod do-set-shape ((w window) shape)
  (when shape
    (set-window shape w)
    (set-delegate shape w))
  (setf (slot-value w 'shape) shape)
  w)

(defmethod set-shape ((w window) shape)
  (send-with-change w 
                    'do-set-shape 'set-shape 
                    `(,shape)))

(defmethod background ((w window))
  (slot-value w 'background))

(defmethod do-set-background ((w window) color)
  (setf (slot-value w 'background) color))

(defmethod set-background ((w window) color)
  (send-with-change w 
                    'do-set-background 'set-background 
                    `(,color)))

(defmethod invalidate ((w window))
  (mg:invalidate (mg-window w))
  w)

(defmethod change ((w window) message changed-obj args)
  (call-next-method)
  (invalidate w))

(defmethod redraw ((window window))
  (let ((mgw (mg-window window)))
    (mg:set-param mgw :background (background window))
    (mg:clear mgw)
    (when (shape window)
      (draw (shape window))))
  window)


;; KlikÃ¡nÃ­

(defmethod find-clicked-shape ((w window) position)
  (when (shape w)
    (find-if (lambda (shape) (contains-point-p shape position))
             (solid-shapes (shape w)))))

(defmethod mouse-down-inside-shape ((w window) shape button position)
  (mouse-down shape button position)
  w)

(defmethod mouse-down-no-shape ((w window) button position)
  w)

(defmethod window-mouse-down ((w window) button position)
  (let ((shape (find-clicked-shape w position)))
    (if shape
        (mouse-down-inside-shape w shape button position)
      (mouse-down-no-shape w button position))))


;; Inicializace

(defmethod install-display-callback ((w window))
  (mg:set-callback (mg-window w)
		   :display (lambda (mgw)
                              (declare (ignore mgw))
                              (redraw w)))
  w)

(defmethod install-mouse-down-callback ((w window))
  (mg:set-callback 
   (mg-window w) 
   :mouse-down (lambda (mgw button x y)
		 (declare (ignore mgw))
		 (window-mouse-down 
                  w
                  button 
                  (move (make-instance 'point) x y)))))

(defmethod install-callbacks ((w window))
  (install-display-callback w)
  (install-mouse-down-callback w)
  w)

(defmethod initialize-instance ((w window) &key)
  (call-next-method)
  (install-callbacks w)
  w)




;;;;;;;;;;;;;
;;;;;;;;;;;;;
;;;;;;;;;;;;;
(defun random-color () 
  (color:make-rgb (random 1.0)
                  (random 1.0)
                  (random 1.0)))


;;ukol 8.1
;sender clicked button position
(defclass click-circle-window (window)
  ())

(defmethod ev-mouse-down ((w click-circle-window) sender clicked button position)
  (when (typep clicked 'circle)
    (progn
      (set-color clicked (random-color))
      (format t "~s~%~s~%~s~%[~s, ~s]~%" sender clicked button (x position) (y position))))
  (call-next-method))

;sender muze byt treba picture, clicked je konkretni kolecko v nem

;8.2
;((obj omg-object) sender msg orig-obj args)

(defclass lockable-window (window)
  ((lockedp :initform nil)))

(defmethod lockedp ((w lockable-window))
  (slot-value w 'lockedp))

(defmethod set-lockedp ((w lockable-window) value)
  (setf (slot-value w 'lockedp) value))

(defmethod ev-changing ((w lockable-window) sender msg orig-obj args)
  (if (lockedp w)
      (progn (format t "Zmena zakazana")
        nil)
    (call-next-method)))


;8.3. Vylepšete tøídu lockable-window tak, aby okna bránila pouze zmìnám objektù
;danıch typù. Zaveïte vlastnost locked-types, která bude obsahovat seznam
;typù (tøíd) objektù, kterım budou zmìny zakázány, pokud je nastavena vlastnost
;lockedp na Pravda. Defaultní hodnota vlastnosti bude (shape), take defaultnì
;budou pøi lockedp rovném Pravda zakázány zmìny všech objektù.

(defclass lockable-window-2 (window)
  ((lockedp :initform nil)
   (locked-types :initform nil)))

(defmethod lockedp ((w lockable-window-2))
  (slot-value w 'lockedp))

(defmethod set-lockedp ((w lockable-window-2) value)
  (setf (slot-value w 'lockedp) value))

(defmethod locked-types ((w lockable-window-2))
  (slot-value w 'locked-types))

(defmethod set-locked-types ((w lockable-window-2) value)
  (setf (slot-value w 'locked-types) value))


(defmethod ev-changing ((w lockable-window-2) sender msg orig-obj args)
  (if (lockedp w)
      (progn (format t "Zmena zakazana")
        nil)
    (if (or (find (type-of sender) (locked-types w))
        (progn (format t "Zmena pro objekt typu ~s zakazana ~s" (type-of sender) orig-obj)
          nil)
      (call-next-method))))








